# 计算机组成原理课程设计

[TOC]

## 1、实验目的与任务

###  1.1 实验目的

1. 融会贯通计算机组成原理课程各章节的内容
   - 通过知识的综合运用加深对计算机系统各模块的工作原理及相互联系的认识，特别是对硬布线控制器的认识 
   - 建立清晰的整机概念
2. 掌握硬布线控制器的设计方法
3. 学习运用可编程逻辑技术进行逻辑设计和调试的基本步骤和方法
   * 熟悉集成开发软件中设计、模拟调试工具的使用
   * 体会可编程逻辑技术相对于传统开发技术的优点
4. 培养科学研究的独立工作能力，取得工程设计与组装调试的实践经验 

### 1.2 实验任务

1. 按照给定的数据格式、指令系统和数据通路，在TEC-8实验台上，设计一个基于硬布线控制器的顺序模型处理器，可以实现如下功能：
   * 对存储器的读写、寄存器指定位置的读写。
   * 可以实现对基本指令的分析，并根据指令操纵相应硬件实现各指令功能
2. 在完成基本功能的前提下，拓展指令集，使芯片可以识别更多的指令
3.  在完成基本功能的前提下，提高指令执行效率，实现流水化
4. 在完成基本功能的前提下，采用指令指针的方式，自定义PC开始位置的内存地址
5. 进一步的，针对所设计的CPU，提出中断实现方案

## 2、实验环境与设备

* TEC-8 控制台一个
* PC一台（Win 7）
* TEC-8 程序下载线

## 3、实验原理图详解

### 3.1 硬布线控制线数据通路图及详解

![1531561868875](C:\Users\WANGXI~1\AppData\Local\Temp\1531561868875.png)

### 3.2 硬布线控制器逻辑模块图及详解

![1531562048278](C:\Users\WANGXI~1\AppData\Local\Temp\1531562048278.png)

### 3.3 硬布线控制器指令周期流程图及详解

* 硬布线控制台操作指令![1531572059617](C:\Users\WANGXI~1\AppData\Local\Temp\1531572059617.png)

* 非流水硬布线原有指令集![](C:\Users\Wang Xin\Documents\Tencent Files\1402543398\FileRecv\硬布线指令系统流程图\硬布线指令系统流程图\非流水硬布线原有指令集.png)
* 流水硬布线原有指令集![](C:\Users\Wang Xin\Documents\Tencent Files\1402543398\FileRecv\硬布线指令系统流程图\硬布线指令系统流程图\流水硬布线原有指令集.png)
* 硬布线扩展指令![](C:\Users\Wang Xin\Documents\Tencent Files\1402543398\FileRecv\硬布线指令系统流程图\硬布线指令系统流程图\硬布线扩展指令.png)

## 4、硬布线控制器源程序及测试详解

### 4.1 硬布线控制器VHDL源程序详解

#### 4.1.1 非流水版本

```vhdl
library ieee;
use ieee.std_logic_1164.all;
use ieee.std_logic_unsigned.all;
use ieee.std_logic_arith.all;
entity nonpipe is
port (
		clr,t3,c,z : in std_logic;
		sw , w : in std_logic_vector (3 downto 1);
		ir : in std_logic_vector (7 downto 4);
		ldz,ldc,cin,m,abus,drw,pcinc,lpc,lar,pcadd,arinc,selctl,memw,stop,lir,sbus,mbus,short,long: out std_logic;
		s,sel :out std_logic_vector(3 downto 0)
);
end nonpipe ;
architecture art of nonpipe is
	signal wreg,rreg,wram,rram,fi,add,sub,aand,inc,ld,st,jc,jz,jmp,stp,dec,xxor,cmp,mov,cla,oout,st0:std_logic;
begin
	wreg <= '1' when sw = "100" else '0';
	rreg <= '1' when sw = "011" else '0';
	wram <= '1' when sw = "001" else '0';
	rram <= '1' when sw = "010" else '0';
	fi <= '1' when sw = "000" else '0';
	oout <= '1' when ir = "0000" else '0';
	add <= '1' when ir = "0001" else '0';
	sub <= '1' when ir = "0010" else '0';
	aand <= '1' when ir = "0011" else '0';
	inc <= '1' when ir = "0100" else '0';
	ld <= '1' when ir = "0101" else '0';
	st <= '1' when ir = "0110" else '0';
	jc <= '1' when ir = "0111" else '0';
	jz <= '1' when ir = "1000" else '0';
	jmp <= '1' when ir = "1001" else '0';
	mov <= '1' when ir = "1010" else '0';
	cmp <= '1' when ir = "1011" else '0';
	cla <= '1' when ir = "1100" else '0';
	dec <= '1' when ir = "1101" else '0';
	stp <= '1' when ir = "1110" else '0';
	xxor <= '1' when ir = "1111" else '0';
	process(clr,w)
	begin
		if(clr = '0')then
			st0 <= '0';
		elsif(falling_edge(t3))then
			if (w(2) = '1' and wreg = '1') or (w(1)='1' and st0 = '0' and (rram = '1' or wram = '1' or fi = '1'))then
				st0 <= not st0;
			end if;
		end if;
	end process;
   ldz<=(((add or sub or aand or inc or dec or xxor or oout)and w(2))or (cmp and w(3))) and fi and st0;
   ldc<=(((add or sub or inc or dec or xxor) and w(2)) or (cmp and w(3))) and fi and st0;
   cin<=(((add or dec or oout) and w(2))or (cmp and w(3))) and fi and st0;
   s(3)<=(((aand or add or ld or jmp or mov or dec or st)and w(2)) or ((cmp or st) and w(3))) and fi and st0;
   s(2)<=(((sub or st or jmp or dec or xxor or cmp) and w(2)) or (cmp and w(3))) and fi and st0;
   s(1)<=(((aand or sub or ld or st or jmp or mov or xxor or cmp or cla or dec) and w(2)) or (st and w(3)) ) and fi and st0;
   s(0)<=(((add or aand or st or jmp or dec or cla) and w(2))) and fi and st0;
   m<=(((aand or ld or st or jmp or mov or cla or xxor) and w(2)) or (st and w(3))) and fi and st0;
   abus<=(((add or sub or aand or inc or ld or st or jmp or oout or mov or cla or dec or xxor or cmp) and w(2)) or (st and w(3))) and fi and st0;
   drw<=((((add or sub or aand or inc or mov or cla or dec or xxor or cmp) and w(2)) or (ld and w(3))) and fi and st0)  or (wreg and (w(1) or w(2)));
   pcinc<= w(1) and fi and st0;
   lpc<= (jmp and w(2) and fi and st0) or (fi and (not st0) and w(1));
   lar<=((ld or st) and w(2) and fi and st0) or ((rram or wram) and (not st0) and w(1));
   pcadd<=((jc and c) or (jz and z)) and w(2) and fi and st0;
   arinc<=(rram or wram) and st0 and w(1);
   selctl <= ((wreg or rreg or rram or wram) and (w(1) or w(2))) or (fi and (not st0) and w(1));
   memw<=(st and w(3) and fi and st0) or (wram and st0 and w(1));
   stop<=(stp and w(2) and fi and st0) or ((wreg or rreg or rram or wram) and (w(1) or w(2)))or(fi and (not st0) and w(1));
   lir<= w(1) and fi and st0;
   sbus<=(wreg and (w(1) or w(2))) or (rram and (not st0) and w(1)) or (wram and w(1))or(fi and (not st0) and w(1));
   mbus<=(ld and w(3) and fi and st0) or (rram and st0 and w(1));
   short <=(rram or wram or (fi and (not st0))) and w(1);
   long<=(ld or st or cmp) and w(2) and fi and st0;
   sel(0)<=(wreg and w(1)) or (rreg and (w(1) or w(2)));
   sel(1)<=(wreg and (not st0) and w(1)) or (wreg and st0 and w(2)) or (rreg and w(2));
   sel(2)<=wreg and w(2);
   sel(3)<=(wreg and st0) or (rreg and w(2));
   -- sel <= "1111";
end art;
```

#### 4.1.2 流水版本

```vhdl
library ieee;
use ieee.std_logic_1164.all;
use ieee.std_logic_unsigned.all;
use ieee.std_logic_arith.all;
entity pipe is
port (
		clr,t3,c,z : in std_logic;
		sw , w : in std_logic_vector (3 downto 1);
		ir : in std_logic_vector (7 downto 4);
		ldz,ldc,cin,m,abus,drw,pcinc,lpc,lar,pcadd,arinc,selctl,memw,stop,lir,sbus,mbus,short,long: out std_logic;
		s,sel :out std_logic_vector(3 downto 0)
);
end pipe ;

architecture art of pipe is
	signal wreg,rreg,wram,rram,fi,add,sub,aand,inc,ld,st,jc,jz,jmp,stp,dec,xxor,cmp,mov,cla,oout,st0:std_logic;
	signal v:std_logic_vector(3 downto 1);
	
begin
	wreg <= '1' when sw = "100" else '0';
	rreg <= '1' when sw = "011" else '0';
	wram <= '1' when sw = "001" else '0';
	rram <= '1' when sw = "010" else '0';
	fi <= '1' when sw = "000" else '0';
	oout <= '1' when ir = "0000" else '0';
	add <= '1' when ir = "0001" else '0';
	sub <= '1' when ir = "0010" else '0';
	aand <= '1' when ir = "0011" else '0';
	inc <= '1' when ir = "0100" else '0';
	ld <= '1' when ir = "0101" else '0';
	st <= '1' when ir = "0110" else '0';
	jc <= '1' when ir = "0111" else '0';
	jz <= '1' when ir = "1000" else '0';
	jmp <= '1' when ir = "1001" else '0';
	mov <= '1' when ir = "1010" else '0';
	cmp <= '1' when ir = "1011" else '0';
	cla <= '1' when ir = "1100" else '0';
	dec <= '1' when ir = "1101" else '0';
	stp <= '1' when ir = "1110" else '0';
	xxor <= '1' when ir = "1111" else '0';
	
	process(clr,w)
	variable u:std_logic_vector(3 downto 1);
	variable opt:std_logic;
	begin
		if(clr = '0')then
			st0  <= '0';
			u :="001";
		elsif(falling_edge(t3))then
			if (w(2) = '1' and wreg = '1') or (w(1)='1' and st0 = '0' and (rram = '1' or wram = '1' or fi = '1'))then
				st0 <= not st0;
			end if;
			opt := ld or st or jmp or (jc and c ) or (jz and z) or cmp or stp;
			u(3) := opt and (not u(1));
			u(2) := u(1) ;
			u(1) := (opt and u(3)) or((not opt) and u(2));
		
		end if;
		v <= u;
	end process;
   
   ldz<=(((add or sub or aand or inc or dec or xxor or oout)and v(2))or (cmp and v(3))) and fi and st0;
   ldc<=(((add or sub or inc or dec or xxor) and v(2)) or (cmp and v(3))) and fi and st0;
   cin<=(((add or dec or oout) and v(2))or (cmp and v(3))) and fi and st0;
   s(3)<=(((aand or add or ld or jmp or mov or dec or st)and v(2)) or ((cmp or st) and v(3))) and fi and st0;
   s(2)<=(((sub or st or jmp or dec or xxor or cmp) and v(2)) or (cmp and v(3))) and fi and st0;
   s(1)<=(((aand or sub or ld or st or jmp or mov or xxor or cmp or cla or dec) and v(2)) or (st and v(3)) ) and fi and st0;
   s(0)<=(((add or aand or st or jmp or dec or cla) and v(2))) and fi and st0;
   m<=(((aand or ld or st or jmp or mov or cla or xxor) and v(2)) or (st and v(3))) and fi and st0;
   abus<=(((add or sub or aand or inc or ld or st or jmp or oout or mov or cla or dec or xxor or cmp) and v(2)) or (st and v(3))) and fi and st0;
   drw<=((((add or sub or aand or inc or mov or cla or dec or xxor or cmp) and v(2)) or (ld and v(3))) and fi and st0)  or (wreg and (w(1) or w(2)));
   pcinc<= v(1) and fi and st0;
   lpc<= (jmp and v(2) and fi and st0) or (fi and (not st0) and w(1));
   lar<=((ld or st) and v(2) and fi and st0) or ((rram or wram) and (not st0) and w(1));
   pcadd<=((jc and c) or (jz and z)) and v(2) and fi and st0;
   arinc<=(rram or wram) and st0 and w(1);
   selctl <= ((wreg or rreg or rram or wram) and (w(1) or w(2))) or (fi and (not st0) and w(1));
   memw<=(st and v(3) and fi and st0) or (wram and st0 and w(1));
   stop<=(stp and v(2) and fi and st0) or ((wreg or rreg or rram or wram) and (w(1) or w(2)))or(fi and (not st0) and w(1));
   lir<= v(1) and fi and st0;
   sbus<=(wreg and (w(1) or w(2))) or (rram and (not st0) and w(1)) or (wram and w(1))or(fi and (not st0) and w(1));
   mbus<=(ld and v(3) and fi and st0) or (rram and st0 and w(1));
   short <=(rram or wram or (fi and (not st0))) and w(1);
   sel(0)<=(wreg and w(1)) or (rreg and (w(1) or w(2)));
   sel(1)<=(wreg and (not st0) and w(1)) or (wreg and st0 and w(2)) or (rreg and w(2));
   sel(2)<=wreg and w(2);
   sel(3)<=(wreg and st0) or (rreg and w(2));
   -- sel <= "1111";
end art;
```

### 4.2 测试程序详解

#### 4.2.1 测试程序一（寻找最大的数）

**4.2.1.1 机器指令描述**

|    地址    |  程序指令   |   机器码    | 16进制 |
| :--------: | :---------: | :---------: | :----: |
|    00H     | LD R1 [R2]  |  01010110   |  56H   |
|    01H     |   INC R2    |  01001000   |  48H   |
|    02H     | LD R0 [R2]  |  01010010   |  52H   |
|    03H     |   OUT R0    |  00000000   |  00H   |
|    04H     |   JZ 09H    |  10000100   |  84H   |
|    05H     |  CMP R0 R1  |  10110001   |  B1H   |
|    06H     |   JC 01H    |  01111010   |  7AH   |
|    07H     | LD R1 [R2]  |  01010110   |  56H   |
|    08H     |  JMP [R3]   |  10011100   |  9CH   |
|    09H     |   OUT R1    |  00000100   |  04H   |
|    0AH     |     STP     |  11100000   |  E0H   |
|            |             |             |        |
| 寄存器初值 |  R2 = 0BH   |  R3 = 01H   |        |
| 存储器初值 | [OBH] = 09H | [0CH] = 08H |        |
|            | [0DH] = 0DH | [0EH] = 02H |        |
|            | [0FH] = 01H | [10H] = 09H |        |
|            | [11H] = 00H |             |        |

**4.2.1.2 测试程序原理描述**

指令和测试数组依次放在各个内存单元中，而对于四个寄存器：$$R_0$$中存储待比较的数字，$$R_1$$中存储目前得到的最大的数字，$$R_2$$作为指针用途，依次遍历内存中的各个数字，由于跳转指令读取地址只能通过寄存器，因此在$$R_3$$中存储每次循环读取时指令的内存地址。

程序每次根据$$R_2$$中的值读取新的数据存入$$R_0$$之中，接着运用拓展指令集中的比较指令比较$$R_0$$和$$R_1$$中的值，若$$R_1$$中的值大，则继续读取下一个数进行比较；若$$R_0$$中的数大，则用新的最大值覆盖现在$$R_1$$中的值，以此类推，知道读到数字0结束程序；

**4.2.1.3 测试结果**

在测试程序中，我们在6个内存单元中分别写入数字9、8、13、2、1、9，程序结束后寄存器$R_1$中的值为13，结果与预测一致，各指令均执行良好；

#### 4.2.2 测试程序二

**4.2.2.1 机器指令描述**

|  **地址**  | **程序指令** | **机器码** | **16**进制 |
| :--------: | :----------: | :--------: | :--------: |
|    00H     |  LD R0 [R2]  |  01010010  |    52H     |
|    01H     |    INC R2    |  01001000  |    48H     |
|    02H     |  LD R1 [R2]  |  01010110  |    56H     |
|    03H     |  ADD R0 R1   |  00010001  |    11H     |
|    04H     |    JC 06H    |  01110001  |    76H     |
|    05H     |  AND R1 R0   |  00110100  |    34H     |
|    06H     |  SUB R0 R2   |  00100010  |    22H     |
|    07H     |    INC R1    |  01000100  |    44H     |
|    08H     | STA R0 [R1]  |  01100100  |    64H     |
|    09H     |    INC R3    |  01001100  |    4CH     |
|    0AH     |    JZ 0DH    |  10000010  |    7DH     |
|    0BH     |  LD R2 [R3]  |  01011011  |    5BH     |
|    0CH     |   JMP [R2]   |  10011000  |    98H     |
|    0DH     |    INC R3    |  01001100  |    4CH     |
|    0EH     |    INC R3    |  01001100  |    4CH     |
|    0FH     |  SUB R0 R2   |  00100010  |    22H     |
|    10H     |  LD R2 [R0]  |  01011000  |    58H     |
|    11H     |  ADD R3 R2   |  00011110  |    1EH     |
|    12H     |  LD R3 [R3]  |  01011111  |    5FH     |
|    13H     |    OUT R0    |  00000000  |    00H     |
|    14H     |     STP      |  11100000  |    C0H     |
|            |              |            |            |
| 寄存器初值 |   R2 = 60H   |  R3 = FDH  |            |
| 存储器初值 |  [60H]=67H   | [61H]=80H  |            |
|            |  [62H]=FDH   | [80H]=60H  |            |
|            |  [FEH]=03H   | [FFH]=03H  |            |

**4.2.2.2 流程模拟**

| 步骤 | 最终结果  | 步骤 | 最终结果 |
| :--: | :-------: | :--: | :------: |
|  1   |  R0=67H   |  12  |  R2=03H  |
|  2   |  R2=61H   |  13  | 转至03H  |
|  3   |  R1=80H   |  14  |  R3=01H  |
|  4   |  R0=86H   |  15  |  R3=02H  |
|  5   |  C=1转移  |  16  |  R0=80H  |
|  6   |  R1=82H   |  17  |  R2=60H  |
|  7   |  R0=83H   |  18  |  R3=62H  |
|  8   |  R1=83H   |  19  |  R3=FDH  |
|  9   | [83H]=83H |  20  |  输出R0  |
|  10  |  R3=00H   |  21  | 停止运行 |
|  11  |  Z=1转移  |      |          |

**4.2.2.3测试结果**

流水方式和非流水方式的测试结果均与预测结果一致，最终结果均为:

寄存器：R0=80H,R1=83H,R2=60H,R3=FDH
存储器：[81H]=86H,[82H]=04H,[83H]=83H

## 5、设计说明书

### 5.1 指令设计

![1531572354338](C:\Users\WANGXI~1\AppData\Local\Temp\1531573786465.png)

* 在此，对自主设计的比较指令CMP原理进行进一步的解释：经过一系列测试，我们发现参与ALU运算的两寄存器均为补码存储，但发现正数与正数的相加＞127后并不会显示溢出或归零（即c，z都不亮），反而负数与负数的相加显示溢出（即z亮）。于是对cmp的设计诞生了一个大胆的想法：cmp有3个指令周期，第一个周期取指令，第二个周期用alu执行sub Rd，Rs，执行后Rd中存放着原Rd与Rs的差值，第三个周期用alu执行A+A，即将Rd值×2，并将结果打到总线上。这个时候可以根据c与z的值来判断原RdRs的大小情况。

  若Rd＞Rs，（Rd-Rs）为正数，2（Rd-Rs）不会溢出，Z=0，C=0；

  若Rd = Rs，（Rd-Rs）为零，2（Rd-Rs）为零。Z=1，C=0；

  若Rd＜Rs，（Rd-Rs）为负数，2（Rd-Rs）必然溢出。Z=0，C=1；

  据此，我们可以通过C、Z的值对寄存器中两个数的大小进行判断；

### 5.2 指令译码

#### 5.2.1 控制台操作译码

|  指令  |             FI(取指令执行指令)              |    WRAM     |    RRAM     |    RREG     |          WREG          |
| :----: | :-----------------------------------------: | :---------: | :---------: | :---------: | :--------------------: |
|  SWC   |                      0                      |      0      |      0      |      0      |           1            |
|  SWB   |                      0                      |      0      |      1      |      1      |           0            |
|  SWA   |                      0                      |      1      |      0      |      1      |           0            |
|  SEL3  |                                             |             |             |    $W_2$    |         $ST_0$         |
|  SEL2  |                                             |             |             |             |         $W_2$          |
|  SEL1  |                                             |             |             |    $W_2$    | $!ST_0*W_1+$$ST_0*W_2$ |
|  SEL0  |                                             |             |             | $ W_1+W_2 $ |         $W_1$          |
|  SBUS  |               $FI*!ST_0*W_1$                |    $W_1$    | $!ST_0*W_1$ |             |       $W_1+W_2$        |
|  DRW   |                                             |             |             |             |       $W_1+W_2$        |
|  STOP  |          $STP*V_2*ST_0+!ST_0*W_1$           |  $W_1+W_2$  |  $W_1+W_2$  |  $W_1+W_2$  |       $W_1+W_2$        |
| SELCTL |                 $ST_0*W_1$                  |  $W_1+W_2$  |  $W_1+W_2$  |  $W_1+W_2$  |       $W_1+W_2$        |
|  LAR   |             $ST_0*(LD+ST)*V_2$              | $!ST_0*W_1$ | $!ST_0*W_1$ |             |                        |
| SHORT  |               $FI*!ST_0*W_1$                |    $W_1$    |    $W_1$    |             |                        |
|  MBUS  |                $LD*V_3*ST_0$                |             | $ST_0*W_1$  |             |                        |
| ARINC  |                                             | $ST_0*W_1$  | $ST_0*W_1$  |             |                        |
|  MEMW  |                $ST*V_3*ST_0$                | $ST_0*W_1$  |             |             |                        |
|  LPC   | $FI*! ST_0*W_1+$$JMP*V_2*FI*ST_0$(流水版本) |             |             |             |                        |

#### 5.2.2 指令译码

|  指令   |  OUT  |  ADD  |  SUB  |  AND  |  INC  |  LD   |    ST     |   JC    |
| :-----: | :---: | :---: | :---: | :---: | :---: | :---: | :-------: | :-----: |
| IR7-IR4 | 0000  | 0001  | 0010  | 0011  | 0100  | 0101  |   0110    |  0111   |
|  PCINC  | $W_1$ | $W_1$ | $W_1$ | $W_1$ | $W_1$ | $W_1$ |   $W_1$   |  $W_1$  |
|   LIR   | $W_1$ | $W_1$ | $W_1$ | $W_1$ | $W_1$ | $W_1$ |   $W_1$   |  $W_1$  |
|    M    | $W_2$ |       |       | $W_2$ |       |       | $W_2+W_3$ |         |
|   CN    |       | $W_2$ |       |       |       | $W_2$ |           |         |
|   S3    |       | $W_2$ |       | $W_2$ |       | $W_2$ | $W_2+W_3$ |         |
|   S2    |       |       | $W_2$ |       |       |       |   $W_2$   |         |
|   S1    |       |       | $W_2$ | $W_2$ |       | $W_2$ | $W_2+W_3$ |         |
|   S0    |       | $W_2$ |       | $W_2$ |       |       |   $W_2$   |         |
|  ABUS   | $W_2$ | $W_2$ | $W_2$ | $W_2$ | $W_2$ | $W_2$ | $W_2+W_3$ |         |
|  MBUS   |       |       |       |       |       | $W_3$ |           |         |
|   DRW   |       | $W_2$ | $W_2$ | $W_2$ | $W_2$ | $W_3$ |           |         |
|   LDZ   |       | $W_2$ | $W_2$ | $W_2$ | $W_2$ |       |           |         |
|   LDC   |       | $W_2$ | $W_2$ |       | $W_2$ |       |           |         |
|  LDPC   |       |       |       |       |       |       |           |         |
|   LAR   |       |       |       |       |       | $W_2$ |   $W_2$   |         |
|  LONG   |       |       |       |       |       | $W_2$ |   $W_2$   |         |
|  PCADD  |       |       |       |       |       |       |           | $W_2*C$ |
|  SHORT  |       |       |       |       |       |       |           |         |
|  MEMW   |       |       |       |       |       |       |   $W_3$   |         |
|  STOP   |       |       |       |       |       | $W_2$ |           |         |

| 指令    | JZ      | JMP   | MOV   | CMP   | CLA   | DEC   | STOP  | XOR   |
| ------- | ------- | ----- | ----- | ----- | ----- | ----- | ----- | ----- |
| IR7-IR4 | 1000    | 1001  | 1010  | 1011  | 1100  | 1101  | 1110  | 1111  |
| PCINC   | $W_1$   | $W_1$ | $W_1$ | $W_1$ | $W_1$ | $W_1$ | $W_1$ | $W_1$ |
| LIR     | $W_1$   | $W_1$ | $W_1$ | $W_1$ | $W_1$ | $W_1$ | $W_1$ | $W_1$ |
| M       |         | $W_2$ | $W_2$ |       | $W_2$ |       |       | $W_2$ |
| CN      |         |       |       |       |       | $W_2$ |       |       |
| S3      |         | $W_2$ | $W_2$ |       |       | $W_2$ |       |       |
| S2      |         | $W_2$ |       | $W_2$ |       | $W_2$ |       | $W_2$ |
| S1      |         | $W_2$ | $W_2$ | $W_2$ | $W_2$ | $W_2$ |       | $W_2$ |
| S0      |         | $W_2$ |       |       | $W_2$ | $W_2$ |       |       |
| ABUS    |         | $W_2$ | $W_2$ |       | $W_2$ | $W_2$ |       | $W_2$ |
| MBUS    |         |       |       |       |       |       |       |       |
| DRW     |         | $W_2$ | $W_2$ |       | $W_2$ | $W_2$ |       | $W_2$ |
| LDZ     |         |       |       | $W_2$ | $W_2$ | $W_2$ |       | $W_2$ |
| LDC     |         |       |       | $W_2$ |       | $W_2$ |       |       |
| LDPC    |         | $W_2$ |       |       |       |       |       |       |
| LAR     |         |       |       |       |       |       |       |       |
| LONG    |         |       |       |       |       |       |       |       |
| PCADD   | $W_2*Z$ |       |       |       |       |       |       |       |
| SHORT   |         |       |       |       |       |       |       |       |
| MEMW    |         |       |       |       |       |       |       |       |
| STOP    |         |       |       |       |       |       |       |       |

### 5.3 指令执行方式设计

#### 5.3.1 原理描述

**5.3.1.1 非流水型**

非流水型指令执行采用串行的方式，虽然有如下4种情况，但是在操作上并没有很大的问题，在此分三个方面进行分析:

| 情况 | 节拍1 | 节拍2 | 节拍3 | 节拍4 | 节拍5 | 节拍6 |
| :--: | :---: | :---: | :---: | :---: | :---: | :---: |
| 1：  | $w_1$ | $w_2$ | $w_1$ | $w_2$ |  ...  |  ...  |
| 2：  | $w_1$ | $w_2$ | $w_1$ | $w_2$ | $w_3$ |  ...  |
| 3：  | $w_1$ | $w_2$ | $w_3$ | $w_1$ | $w_2$ |  ...  |
| 4：  | $w_1$ | $w_2$ | $w_3$ | $w_1$ | $w_2$ | $w_3$ |

① 操作台模式的设计

我们组采用了组合逻辑，将每一个微操作所需要的时序提取出来并加以组合。SWC、SWB、SWA的000、001、010、011、100分别代表取指执行、写存储器、读存储器、读寄存器、写寄存器，并用FI、WRAM、RRAM、RREG、WREG信号来代表当前所进行的控制台操作。每条指令有两个或三个周期，分别用$W_1、W_2、W_3$来表示，这样每条操作都可以用相应的操作台控制模式、指令以及相应的指令周期来组合而成。

② 关于$st_0$的使用设计：

老师在课上说过，st0是难点，对此我们进行了反复的推敲和实验，最终确定了如下的方式：$st_0$在初始化时以及按下clr时为0，在写寄存器和读寄存器时的$w_2$阶段会进行反转，在取值打入PC、读寄存器打入AR以及写寄存器打入AR的$W_1$阶段也会进行反转。然而在取值、读写存储器时若$st_0=1$，则除非按下clr否则不会反转，如此一来，便成功使用了$st_0$且不会因$st_0$的无故反转而影响后续操作。

③ 关于指令集的设计：

由于采用组合逻辑，指令集的设计的重点为相应的逻辑表达式，通过探究节拍电位有效时哪些信号应该有效，仔细写出逻辑表达式并将其变换成可执行的VHDL语言，便可以成功完成设计。（PS：关于指令扩展详情见于指令设计表）

**5.3.1.2 流水型**

设计的所有指令都是两周期或者三周期，因此在进行流水并行执行时只会出现以下4种情况：

| 情况 | 节拍1 | 节拍2 | 节拍3 | 节拍4 | 节拍5 |
| :--: | :---: | :---: | :---: | :---: | :---: |
|  1:  | $v_1$ | $v_2$ |       |       |       |
|      |       | $v_1$ | $v_2$ |       |       |
|      |       |       |       |       |       |
|  2:  | $v_1$ | $v_2$ |       |       |       |
|      |       | $v_1$ | $v_2$ | $v_3$ |       |
|      |       |       |       |       |       |
|  3:  | $v_1$ | $v_2$ | $v_3$ |       |       |
|      |       |       | $v_1$ | $v_2$ | $v_3$ |
|      |       |       |       |       |       |
|  4:  | $v_1$ | $v_2$ | $v_3$ |       |       |
|      |       |       | $v_1$ | $v_2$ |       |

在设计流水时，我们主要在两个关键点上进行了设计：

* 将下一条指令的取指令周期$v_1$接在上一条命令的最后一个周期

理由如下：前指令为周期为2的指令（以下简称周期为n的指令为“n指令”）时自然如此，为3指令时，若三指令$v_2$与下一条指令的$v_1$同时执行，则等到3指令执行$v_3$时,2指令执行$v_2$，此时指令寄存器已经修改，根据组合逻辑，在前指令与后指令不同时，IR7~IR3改变，3指令$v_3$不能正常执行。

* 此处流水只能设计为两条流水，而无法拓展到三条流水或更多

理由如下：如果三条流水，那么意味着同一时刻内执行着3个周期的任务。而并不是所有的指令都是3周期的。一旦三条流水遇到连续的2周期指令，那么必定会出现两个以上的$v_2$或者$v_1$，都会引起资源冲突。并且由此推断指令周期的最小值是流水跟流水线数的最大值相关。

**5.3.1.3 两种设计模式比较分析**

在此主要从两个点上对比分析：

* 节拍电位

非流水的节拍电位为硬件提供的输入。其联系密切，并且在取值执行过程中，会有long信号辅助产生$w_3$.

为了兼顾多条流水线，流水信号的节拍电位并不是直接由硬件w输入直接提供（因为w只能一次一位有效）。本cpu设计为：取消long，且使硬件w输入信号与节拍电位无关，即w在w= “001”与“010”反复周折，同时用信号v来模拟w的电位产生，而v信号的结果只与前一状态执行的指令类型和前一周期信号的真假相关。

具体的实现逻辑如下：

```vhdl
process(clr,w)
    variable u:std_logic_vector(3 downto 1);
    variable opt:std_logic;
    begin
    if(clr = '0')then
    st0  <= '0';
    u :="001";
    elsif(falling_edge(t3))then
    if (w(2) = '1' and wreg = '1') or (w(1)='1' and st0 = '0' and (rram = '1' or wram = '1' or fi = '1'))then
    st0 <= not st0;
    end if;
    opt := ld or st or jmp or (jc and c ) or (jz and z) or cmp or stp;
    u(3) := opt and (not u(1));
    u(2) := u(1) ;
    u(1) := (opt and u(3)) or((not opt) and u(2));

    end if;
    v <= u;
end process;

```

注：

时序逻辑中，另设变量u来进行同步执行。Process结束后再将u值统一赋值给v信号。以下注释中可将u，v理解为同一事物。 clr清零时，执行变量u的初始化 “001“，即只有$v_1$有效。当t3下降沿时，执行

```vhdl
opt := ld or st or jmp or (jc and c ) or (jz and z) or cmp or stp;
u(3) := opt and (not u(1));
u(2) := u(1) ;
u(1) := (opt and u(3)) or((not opt) and u(2));
```

其中opt变量记录的是当前指令是否为3指令。若是则为真。

而u(3)为真的条件是：当前指令为3指令，且前一个周期中$v_1$不为真（尝试过$v_2$为真，命题是等价的，但是实际操作中会出现错误）

再判断u(2)，若前一周期$v_1$有效，$v_2$必然有效；

​最后判断u(1)，在u(3)、u(2)都修改后，如果当前指令为3指令且u3为真如下图1，或当前指令为2指令且u2为真如下图2，u1为真。                         

| $v_2$ |
| ----- |
| $v_1$ |
| 图2   |

* 由流水变成非流水，指令需要有哪些变化？·

在v模拟w的设计下，为了保证避免pcadd，pcinc，lpc等信号的冲突，我们将jmp，jc，jz，stp理解为3指令，其中第三个指令周期虽然为空，但是可以有效避免冲突。同时，不再需要long信号进行区分。

(此处附上流水版的硬布线控制器参考流程图)

#### 5.3.2 逻辑表达式

```vhdl
   LDZ=(((ADD + SUB + AAND + INC + DEC + XX+ + OOUT)& W(2))+ (CMP & W(3))) & FI & ST0;
   LDC=(((ADD + SUB + INC + DEC + XX+) & W(2)) + (CMP & W(3))) & FI & ST0;
   CIN=(((ADD + DEC + OOUT) & W(2))+ (CMP & W(3))) & FI & ST0;
   S(3)=(((AAND + ADD + LD + JMP + MOV + DEC + ST)& W(2)) + ((CMP + ST) & W(3))) & FI & ST0;
   S(2)=(((SUB + ST + JMP + DEC + XX+ + CMP) & W(2)) + (CMP & W(3))) & FI & ST0;
   S(1)=(((AAND + SUB + LD + ST + JMP + MOV + XX+ + CMP + CLA + DEC) & W(2)) + (ST & W(3)) ) & FI & ST0;
   S(0)=(((ADD + AAND + ST + JMP + DEC + CLA) & W(2))) & FI & ST0;
   M=(((AAND + LD + ST + JMP + MOV + CLA + XX+) & W(2)) + (ST & W(3))) & FI & ST0;
   ABUS=(((ADD + SUB + AAND + INC + LD + ST + JMP + OOUT + MOV + CLA + DEC + XX+ + CMP) & W(2)) + (ST & W(3))) & FI & ST0;
   DRW=((((ADD + SUB + AAND + INC + MOV + CLA + DEC + XX+ + CMP) & W(2)) + (LD & W(3))) & FI & ST0)  + (WREG & (W(1) + W(2)));
   PCINC= W(1) & FI & ST0;
   LPC= (JMP & W(2) & FI & ST0) + (FI & (! ST0) & W(1));
   LAR=((LD + ST) & W(2) & FI & ST0) + ((RRAM + WRAM) & (! ST0) & W(1));
   PCADD=((JC & C) + (JZ & Z)) & W(2) & FI & ST0;
   ARINC=(RRAM + WRAM) & ST0 & W(1);
   SELCTL = ((WREG + RREG + RRAM + WRAM) & (W(1) + W(2))) + (FI & (! ST0) & W(1));
   MEMW=(ST & W(3) & FI & ST0) + (WRAM & ST0 & W(1));
   STOP=(STP & W(2) & FI & ST0) + ((WREG + RREG + RRAM + WRAM) & (W(1) + W(2)))+(FI & (! ST0) & W(1));
   LIR= W(1) & FI & ST0;
   SBUS=(WREG & (W(1) + W(2))) + (RRAM & (! ST0) & W(1)) + (WRAM & W(1))+(FI & (! ST0) & W(1));
   MBUS=(LD & W(3) & FI & ST0) + (RRAM & ST0 & W(1));
   SH+T =(RRAM + WRAM + (FI & (! ST0))) & W(1);
   LONG=(LD + ST + CMP) & W(2) & FI & ST0;
   SEL(0)=(WREG & W(1)) + (RREG & (W(1) + W(2)));
   SEL(1)=(WREG & (! ST0) & W(1)) + (WREG & ST0 & W(2)) + (RREG & W(2));
   SEL(2)=WREG & W(2);
   SEL(3)=(WREG & ST0) + (RREG & W(2));
```

### 5.4 关于指令执行阶段(FI)指针的设计 

书中流程图中所设计的没有指针，因而PC会从00H开始取指并执行指令，我们设计了指针，在取指令之前输入地址打入PC，如此一来便可以从任意地址开始取指令并执行指令。我们相应的设计为：使用了其他控制台应用程序也用到的st0信号，st0=0表示为打入PC阶段，打入完成后，st0反转，进入顺序取指执行阶段，除非按下clr，否则st0会一直等于1，不再进行手动打入PC操作，因而不会影响指令的执行。

指针的微操作设计图:

当st0=0时有：

![img](C:\Users\WANGXI~1\AppData\Local\Temp\1531643509711.png)

### 5.5 引脚连接

#### 5.5.1 设计图

![img](https://qqadapt.qpic.cn/txdocpic/0/4a298a3877d8a2e43b4e2629c6c4c3fc/0) 

#### 5.5.2 管脚连接

![1531573946071](C:\Users\WANGXI~1\AppData\Local\Temp\1531573946071.png)

### 5.6 中断方案

单级中断方案：

首先应该明晰，在按下中断时，不能立即进行中断，因为当前的指令可能并没有执行完毕，仅仅完成了取值，因此若按下中断，应当延迟到该指令执行完成再进行中断。我们可以设中断按下时变量pauseflag=1，当pauseflag=1且指令执行完毕即当前电位为$w_1$时将变量pause信号置为1，pauseflag=0表示开始执行中断，并将pauseflag重置为0。

由于是单级中断方案，中断的步骤为关中断->保护断点->保护现场->中断服务->恢复现场->开中断->中断返回。

关中断可设计为：当pause=1，pauseflag=0时表示关中断，此时按下中断按键则pauseflag将仍为0，表示不接受新的中断，即中断挂起状态；

保护现场：用特定的存储器地址记录当前的指令地址，再用另外的四个存储器地址分别表示4个寄存器的数值。

中断服务：在执行中断服务子程序时，将pause信号与中断服务子程序的操作相与，将not pause信号与正常程序相与，即可执行中断服务子程序。

恢复现场：中断服务子程序执行完毕后将4个特定存储器地址中的数值分别打入4个寄存器。开中断：将pauseflag和pause信号都置为0，表示可以接收新的中断指令，此时按下中断则可以将pauseflag置为1并在$w_1$阶段将pause置为1，pauseflag置为0进入新的中断。

中断返回：若没有新的中断则将存储器中特定位置所存储的指令地址打入PC以继续取指令执行指令阶段。

如此设计，便可以设计出单级中断方案。

## 6、调试与改进

**Q1：在运行非流水硬布线控制器时执行测试程序，发现JMP指令执行异常PC无法正确跳跃到指定地址。**

>  A1：通过对微操作表达式进行检查，发现表达式错误，对其进行修改之后JMP能够正常执行。



**Q2：阅读教材上给出SUB的指令产生疑问：当S=0110，M=0时，74181ALU芯片执行的指令时A-B-1而非A-B。**

>  A2：通过在网上搜索74181ALU的运算功能表，发现ALU在M=0时通过进位信号CIN对-1操作进行划分。当CIN=1时，执行-1操作；当CIN=0时不进行-1操作。



**Q3：当设计CMP（比较）指令时，想通过74181ALU的A-B功能实行。但发现74181ALU执行的是补码运算，无法通过读取反馈值C和Z来进行比较判断。**

> A3：我们发现令A-B=D，当D<0时，D的首位值为1；当D>0时，D的首位值为0。我们可以进行2D的运算，则当D<0时，2D发生溢出即C=1，Z=0;当D>0时，C=0，Z=0；当D=0时，C=0，Z=0.则通过对C和Z取值进行判断，完成对A和B 的比较.

| A-B=D的取值 | D的存储值 | 有无溢出 | C    | Z    |
| ----------- | --------- | -------- | ---- | ---- |
| D<0         | 1XXXXXXX  | 有       | 1    | 0    |
| D=0         | 0XXXXXXX  | 无       | 0    | 1    |
| D>0         | 00000000  | 无       | 0    | 0    |

 

**Q4：当运行流水硬布线控制器时，发现JMP指令仍然无法很好的执行，即PC寄存器的值没有进行正确的改变。**

>  A4：我们通过仔细阅读教材上的指令流程图，发现JMP指令与IF（取指指令），可能存在数据相关的写后读（RAW）相关，即正确顺序应该时JMP指令先修改PC值之后再执行IF读取PC值的操作。经过一番讨论后，我们决定将JMP设置为三节拍电位的指令，则在JMP的v2电位的下降沿执行PC修改操作，在v3电位执行下条指令的取指操作。发现经过修改之后JMP指令运行良好，我们将JC和JZ操作也设定为三节拍电位的操作。

 

**Q5：扩展指令时发现MOV，DEC和XXOR指令无法正常运行。**

> A5：重新审视扩展指令的微操作的表达式，发现表达式错误和指令设计错误。经过修改后该3条指令能够正常运行。

 

**Q6：在执行ST（存存储器）操作时，程序能够正常执行，但是寄存器中的数据并没有打入指定存储器的地址中去。**

> A6：在反复运行 ST R0 [R1] 操作后，我们发现当位于该指令的w2周期时数据通路上显示的值并不是寄存器R1中的值，而是寄存器R0中的值，即打入AR寄存器中的值并不是R1中的地址。在检查翻译后的机器码后，发现机器码是01100001而不是01100100。经过修改机器码后指令运行正常。

 

**Q7：指针的设计问题解决**

> A7：对于修改PC数据问题：一开始我们限制与老师所说的PC对于程序员是透明的教导，一直不知道如何对PC值修改。后来通过认真阅读JMP指令发现该指令是通过LPC对PC进行操控并修改取值，意识到我们并不是普通的软件程序员也并不是在做软件。设计底层硬件时当然可以通过对开关的操作修改PC值，所谓的透明性仅仅是对软件程序员而言。
>
> 对于节拍电位问题：我们运用教材上的写寄存器和读写存储器的节拍电位设计思想，运用st0变量，将w1电位扩展为两节拍。在st0=0时，对PC进行修改；在st0=1是进行指令读取。指针的流程图如下。

![img](file:///C:/Users/WANGXI~1/AppData/Local/Temp/msohtmlclip1/01/clip_image001.png)

 

**Q8：流水硬布线控制器信号延迟问题的解决。**

> A8：通过学习VHDL语言我们知道信号是存在延迟问题，而在原来的设计方法中，我们是在T3信号的下降沿对节拍电位的值v1\~v3进行修改的，由于信号的延迟，v1\~v3的值可能并不是全部通过旧的v1\~v3的值所得到的，即可能v2的值由于延迟短先进行变化而后转而去修改v1或v3的值。为了解决此问题我们利用VHDL语言中变量的瞬时变化性，引入变量u1~u3，在T3的下降沿对变量进行改变，而后用变量向量u对信号向量v进行赋值。

 

**Q9：在阅读CPU的指令系统表格，对JC和JZ的指令功能中的偏移量offset的编码方式产生疑问。**

> A9：经过对JC和JZ问题进行测试后发现，offset是补码编码的。

 

**Q10：在设计STP（停机）指令时，发现在执行STP指令之后PC寄存器的值会指向STP地址的后两位地址。**

> A10：面对该问题，我们一开始大胆假设原因是停机操作的STOP操作和下一条指令取指的PCINC操作同时进行，而PCINC操作先于STOP操作，导致PC值在STOP地址的基础上递增两次。通过将STP指令设计为三节拍电位的实验，发现此时PC值指向正常，于是我们便将STOP指令也设计为三节拍电位指令。

## 7、特色功能描述与实验总结

### 7.1 特色功能

* 实现了流水执行指令，极大的提高了指令执行的效率
* 拓展了指令集，在基本指令集的基础上实现了比较寄存器中数据大小等特色指令，并据此设计了寻找最大数的测试程序
* 实现了程序指针，使得PC开始的值不再局限在00H处，可由操作人员自己指定
* 对中断的实现提出了创新方案

### 7.2 实验总结

## 8、心得与体会

**卢昱昊**

**王昕**

**窦宇豪**

**董冠辰**

## 8、 实验日志

**8.1 Day 1**

第一天我们并没有实施任何的代码，我们测试了数据通路是否完好（即重新做了一遍倒数第二次实验），我们一整天都在讨论该实验大体应该如何去完成。一开始我们有个疑惑是我们并不知道编程的时候我们的权限是哪一层，最底层的设想甚至是以为要给硬件每个引脚以高低电平来进行数据传送，高层点的设想是以为数据可以看作十进制直接进行运算，就像c语言的编程。经过小组成员的讨论以及我们对VHDL语言的回顾，我们明白了编程应该操作的是信号、电位等。

首先我们确定了编程实现的方式：在比较了时序逻辑型和组合逻辑型后，我们认为时序逻辑代码过于冗长且有很多无意义的重复，虽然思想简单但是不利于检错纠错，与c语言编程很类似但是总觉得和硬件格格不入，且会拖慢编程的进度；与之相反，存储逻辑型则很符合计组教材上微操作部分的讲解，一来和教材所学相吻合，二来很有硬件编程的特色和味道，虽然在确定逻辑表达式时应分外小心保证万无一失，但代码肯定会较短，一目了然。于是我们决定使用组合逻辑。最后我们将课程设计分成了如下几个模块依次攻克：

一、制定指令译码表

二、完成非流水线版本

三、完成流水线版本

四、进行创新(拓展指令集、指令指针等)，设计中断方案

**8.2 Day 2**

第二天我们采用组合逻辑，首先有的组员进行了组合逻辑表达式的书写，经过合并和反复检查，再变换为VHDL语言，我们初步完成了非流水版本代码的主体。关于st0的使用以及反转问题我们进行了反复的实验、讨论和改进，最后确定了st0的使用方式。完成了上述工作之后，已经接近下午6点，实验室即将关门，我们的程序还有些许bug，且对于流水线版本到底如何流水我们仍是一头雾水。

晚上6点-10点，我们组成员预约了图书馆研讨间，并就指令扩展问题和流水线版本的思想和实现问题进行了思考和讨论。

关于流水线的讨论：我们首先注意到，每条指令或是二电位指令，或是三电位指令，至于这两种周期的指令如何组合、流水设计问题我们讨论出了两种方案：

将流水线版本的指令周期都统一为三电位指令，如此一来，对于二电位指令，将会有一个轮空的三周期，且任何指令的取指阶段都在上一条指令的三周期。这样的话我们所设计的其实是一个半流水的cpu，只有当上一个指令是三周期的时候我们才是真正实现了并行操作（即在上一条指令的执行阶段进行了下一条指令的取值），若上一条指令为二周期指令则在时空利用上与非流水版本无异，因此相当于半流水的cpu。但是我们主要到三周期的指令在不扩充的情况下仅仅只有两条，这种设计无疑在执行过程中和非流水版本的时空利用大体相当。

鉴于上述设计，我们进行了改进，将流水线版本的指令周期统一设计去除，在读写寄存器、读写存储器时仍然使用w1、w2、w3作为电位信号，在取指执行阶段重新定义了电位信号v1、v2、v3，且v1、v2、v3中可以有一个或两个同时有效，即并行操作。我们确定了v1、v2、v3的变化方式由当前指令的长短和当前的v信号来确定。

**8.3 Day 3**

第三天我们对第二天中非流水版本没有解决的bug进行了修复，对取指之前的指针进行了设计：

1. 最开始我们以为PC是硬件设计好的，我们无权限直接操作PC，于是我们的想法是00地址存一个JMP指令来实现跳转，后发现这种设计其实是不符合要求的，它强迫使用者更改自己的指令，并非硬件上提供了支持。
2. 后来我们的想法是输入地址后把SBUS打开，并打入寄存器，再将地址打入ALU，并不执行运算最后将结果通过总线打入PC，但发现其实是多此一举。
3. 最终我们的想法是输入指针的地址之后直接SBUS有效，此时可直接将指针的地址通过总线打入PC，简单高效，再附加SHORT命令不进入w2电位，将其付诸实践，非常成功。
4. 最终的实现效果是，取指时先通过数据总线打入取指地址，再按QD则直接从该打入地址开始依次执行指令。
5. 今天我们又尝试实现流水，在时序逻辑中，我们采用的是消去long，借助自定义信号v来代替输入w，并通过组合v的逻辑来实现流水。具体实现原理见本报告流水部分。

此设计中最重要的是时序部分的设计。指令部分只需要取指部分w改为v。时序部分我们希望达到的效果是先给opt（当前指令是否为3周期指令）赋值，再给v3赋值，再根据改后的v3改v2，再根据修改后的v3.v2.opt 修改v1。最开始使用v1-v3是使用信号signal。但在实验中发现signal的赋值是有延迟的，经常会发生v3的赋值在v2之后，发生的错误导致取指令都无法进行。然后尝试让v3的改变与v2无关，改成与v1有关，逻辑上等价。但是问题并没有得到根本的解决，部分指令取指依然存在问题。

这个时候我们开始试图利用t3上升沿，将v1放在t3下降沿处改变，其余放在上升沿。发现流水线连续取指部分基本成功。但是指令的v2依然有问题。

**8.4 Day 4**

今天我们一位同学去设计扩展指令集，一位同学继续改进流水。一位同学尝试去用扩展后的指令实现一个可以取寄存器里连续地址中的最大数的程序。

流水部分，认为signal只有process结束才改变，有延迟，难以胜任该顺序执行且变量间赋值的任务。了解到variable可以立刻改变，于是在process中设置了variable u来代替所有的v，发现部分指令的v2没有问题。如inc，sub。于是认为流水实现了。（但是后期测试ld，st时发现需要v3的指令，v3并没有执行成功。详见Day 5）

扩展指令集的同学，按照已有的指令设计了dec（减1），xxor（异或），cla（清零），out（输出到总线），mov（寄存器赋值给寄存器）的信号逻辑关系，这些指令被很快的实现。但是因为实现找最大数的程序需要cmp（比较）函数，这个功能alu又没有直接给出，所以需要自己进行设计。其具体设计思路在5.1指令设计部分有着详细的解释。

cmp指令设计好后，设计找最大数程序的同学开始根据cmp的特性设计好了该程序。具体的设计在测试程序的原理解释部分已经有了详细的描述。事实上，根据这个思路，可以实现选择排序等排序算法。

设计好后，我们讲扩展指令融入了非流水与流水的代码中。并在非流水的状态下测试并debug了所有扩展指令。程序测试环节留给了明天。

**8.5 Day 5**

虽然今天老师没要求我们来实验室，但是我们的任务并没有完成，所以今天我们小组成员都来进行最后的工作。前一天我们设计出的寻找几个数中的最大数的机器指令程序还没有进行测试，以及jc、jz、jmp、ld、st指令没有测试，上午我们进行了这些测试工作。

1、首先针对每个指令，我们都设计了简短的测试程序，并执行来检测所测指令是否有误，经检验，我们的程序没有错误。

2、关于JC、JZ的offset，我们进行了研究，发现在w2阶段，pc会自动+1，此时pc所指为当前指令地址的下一地址，若向后跳转，则偏移量的绝对值需要-1，若向前跳转，偏移量绝对值需要+1。

3、关于offset的码制，我们并不清除是原码、反码还是补码，因此进行了实验，最终确定了4位的offset为补码，第一位为符号位，于是我们对写的汇编检测程序中的JC、JZ的机器码进行了相应的修改。

4、我们用非流水线版本对求最大数的程序进行了检验，最终特定寄存器中出现了要求的最大数，实验很成功。

下午：

5、在使用流水线版本进行程序检测时，出现了突发状况，即发现INC指令并没有执行，寄存器数值并没有改变。于是进行了痛苦的Debug过程。最终发现，问题出现在是流水线版本的指令上升沿下降沿的分配上。

实际上一系列的执行指令失败的原因，是在于敏感信号w改变时，t3的上升沿已经错过。昨天将process里的v改为增如variable u后，保留了上升沿的设计没改变，实际上用了variable后，用上升沿强行时序已经没有意义。于是opt与u3 u2 u1的改变都放在了t3下降沿。最后在process结束统一将u赋值给v。从根本上解决了流水问题。流水时序真正成功。

6、在流水测试中发现三周期opt不仅仅是st ld与cmp，jz，jc当z或c=1执行pcadd会与取指时的pcinc冲突；jmp的lpc也会与取指时的pcinc冲突，stp在执行停止后并没有必要再去取指令了。所以三周期的opt逻辑从cmp= st or ld or cmp 改为 cmp=st or ld or cmp or stp or jmp or（jz and z）or （jc and c），整个流水彻底成功。

7、最后我们用老师的测试程序对流水线版本和非流水线版本进行测试，发现居然还有bug，最后发现是机器码表有错误，因为之前进行过指令的扩充，有的指令机器码改变过，而所用的真值表里机器码没有更新。更新之后，得到的结果与其他组一致，可以认为成功完成了实验阶段。

8、针对中断方案我们做了相应的思考，根据计组教材中关中断->保护断点->保护现场->中断服务->恢复现场->开中断->中断返回的步骤，我们提出了自己的设计方案。

**8.6 Day 6**

我们对这几天所作的工作进行了整理和汇总，将各种电路图译码表逐一汇总，对文档进行了整体完善。并对程序进行了最后的测试与完善。







